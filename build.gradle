apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'

description = 'LBU-LTI-ToolApi'
group = 'io.github.leeds-beckett-digital-learning'


/**
 * This is here because the LTI dependency may be a snapshot version.
 * Changing the resolution strategy means the ToolApi will be updated even
 * when the snapshot version hasn't changed.
 **/
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


compileJava {
    options.compilerArgs << '-Xlint:unchecked' 
}

// Publish javadoc and source packages along with built library
java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'javax.servlet:javax.servlet-api:4.0.0'
    implementation 'javax.websocket:javax.websocket-api:1.1'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.apache.commons:commons-text:1.12.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.2'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'

    implementation( 'io.github.leeds-beckett-digital-learning:lbu-lti:0.1.5.SNAPSHOT' )
    { changing = true }

    // The generic java cache API is included here but the standard
    // Java runtime will not have a cache provider installer so an
    // application that uses this library will need to include a
    // cache provider.
    implementation 'javax.cache:cache-api:1.1.1'


    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
}

signing {
    sign(publishing.publications)
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
      pom {
        name = rootProject.name
        description = 'A Java library to support the creation of LTI 1.3 tool sets'
        url = 'https://github.com/leeds-beckett-digital-learning/lbu-lti-toolapi'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'maber01'
                name = 'Jon Maber'
                email = 'j.r.maber@leedsbeckett.ac.uk'
            }
        }
        scm {
            connection = 'scm:git:https://github.com/leeds-beckett-digital-learning/lbu-lti-toolapi.git'
            developerConnection = 'scm:git:git@github.com:leeds-beckett-digital-learning/lbu-lti-toolapi.git'
            url = 'https://github.com/leeds-beckett-digital-learning/lbu-lti-toolapi'
        }
      }
    }
  }
  repositories {
    maven {
      name = "OSSRH"
      url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = ossrhUserName
        password = ossrhToken
      }
    }
  }
}