apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'


description = 'LBU-LTI-ToolApi'
group = 'uk.ac.leedsbeckett'

/**
 * This is here because the LTI dependency may be a snapshot version.
 * Changing the resolution strategy means the ToolApi will be updated even
 * when the snapshot version hasn't changed.
 **/
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


compileJava {
    options.compilerArgs << '-Xlint:unchecked' 
}

// Publish javadoc and source packages along with built library
java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
      url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-lti"
      credentials {
        username = gitHubUserName
        password = gitHubPrivateToken
      }
    }
}

dependencies {
    implementation 'javax.servlet:javax.servlet-api:4.0.0'
    implementation 'javax.websocket:javax.websocket-api:1.1'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.0'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation( 'uk.ac.leedsbeckett:lbu-lti:0.0.25.SNAPSHOT' )
    { changing = true }

    // The generic java cache API is included here but the standard
    // Java runtime will not have a cache provider installer so an
    // application that uses this library will need to include a
    // cache provider.
    implementation 'javax.cache:cache-api:1.1.1'


    testImplementation     'junit:junit:4.13'
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-lti-toolapi"
      credentials {
        username = gitHubUserName
        password = gitHubPrivateToken
      }
    }
  }
}